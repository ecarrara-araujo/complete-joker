ext {
    ANDROID_SUPPORT_LIBS_VERSION = '26.1.0'
    CONSTRAINT_LAYOUT_VERSION = '1.0.2'
    PLAY_SERVICES_ADS = '11.4.2'
    ANDROID_TEST_SUPPORT_LIBS_VERSION = '1.0.1'
    JUNIT_VERSION = '4.12'
    ESPRESSO_VERSION = '3.0.1'
    RETROFIT_VERSION = '2.1.0'
    OKHTTP_VERSION = '3.4.1'
    DAGGER_VERSION = '2.10'
}

buildscript {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath "com.google.cloud.tools:appengine-gradle-plugin:1.3.3"
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
}

final E2E_TESTING_TASK_NAME = "End to End Testing"

task endToEndTest {
    description "Complete Joker App Testing with local Joker Api server"
    group E2E_TESTING_TASK_NAME
    dependsOn = [":joker-api:appengineStart", "runConnectedTests"]
    finalizedBy ":joker-api:appengineStop"
}

task runConnectedTests {
    description "Run application tests"
    group E2E_TESTING_TASK_NAME
    dependsOn ":app:connectedCheck"
    shouldRunAfter ":joker-api:appengineStart"
    doLast { println "Tests have Run!" }
}
